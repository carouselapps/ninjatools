;;;; Copyright Â© 2015 Carousel Apps, Ltd. All rights reserved.

(ns ninjatools.routes
  (:require [clojure.set :refer [rename-keys]]
            [domkm.silk :as silk]
            [pushy.core :as pushy]
            [re-frame.core :as re-frame]))

(def routes (silk/routes [[:home [[]]]
                          [:tools [["tools"]]]
                          [:tool [["tool" :slug]]]
                          [:about [["about"]]]]))

(defn sanitize-silk-keywords [matched-route]
  (rename-keys matched-route {:domkm.silk/name    :name
                              :domkm.silk/pattern :pattern
                              :domkm.silk/routes  :routes
                              :domkm.silk/url     :url}))

(defn parse-path [path]
  (sanitize-silk-keywords (silk/arrive routes path)))


(defn dispatch-route [matched-route]
  (let [event-name (keyword (str "display-page-" (name (:name matched-route))))]
    (re-frame/dispatch [event-name matched-route])))

(defn start! []
  (pushy/start! (pushy/pushy dispatch-route parse-path)))

(def url-for (partial silk/depart routes))
